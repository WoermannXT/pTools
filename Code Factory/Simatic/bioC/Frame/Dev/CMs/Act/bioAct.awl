FUNCTION "bioAct" : Void
TITLE = Batch I/O Actuator Processing
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Woermann
VERSION : 0.4
// Batch I/O Actuator Processing
// by Woermann Automation
// Date: 2020-07-15
// Description:


	VAR_INPUT 
	END_VAR

	VAR_OUTPUT 
	END_VAR

	VAR_IN_OUT 
		me : "bioAct.DT";
	END_VAR

	VAR_TEMP
		_TBase : Real;	// Time Base (Block Cycle Time in s)
		_ACO : Bool;	// Internal Automatic Control On		
		_OOn : Bool;	// Internal Output On		
		_OOff : Bool;	// Internal Output Off		
		_OnTd : Bool;	// Delay On Timer Done	
		_OffTd : Bool;	// Delay Off Timer Done	
		_AlmTd : Bool;	// Alarm Timer Done	
	END_VAR


BEGIN
	NETWORK
	TITLE = Initialise
		SET;
	// -- Get Time Base in Seconds
		L	"bioCDT.DB".SysTime.dtS;
		T	#_TBase;


	NETWORK
	TITLE = Type Definition
		SET;
	// --- Type Selection -----------------------
		L	#me.Typ;
		JL    NDEF;
		JU    Typ0;	// Output On
		JU    Typ1;	// Output On; Feed back Off
		JU    Typ2;	// Output On; Feedback Off (Regulation Valve)
		JU    Typ3;	// Output On; Feedback On
		JU    NDEF;	
		JU    Typ5;	// Output On; Feedback On + Off 
	NDEF: BEU;
	Typ0:A	#me.OOn;
		=	#me.On;
		NOT;
		=	#me.Off;
		CLR;
		=	#me.MOff;
		=	#me.MOn;
		JU	TEND;
	Typ1:A	#me.FOff;
		AN	#me.OOn;
		=	#me.Off;
		AN	#me.FOff;
		AN	#me.OOn;
		=	#me.MOff;
		
		AN	#me.FOff;
		A	#me.OOn;
		=	#me.On;
		A	#me.FOff;
		A	#me.OOn;
		=	#me.MOn;
		JU	TEND;
	Typ2:A	#me.FOff;
		AN	#me.OOn;
		=	#me.Off;
		AN	#me.FOff;
		AN	#me.OOn;
		=	#me.MOff;
		
		A	#me.OOn;
		=	#me.On;
		CLR;
		=	#me.MOn;
		JU	TEND;
	Typ3:AN	#me.FOn;
		AN	#me.OOn;
		=	#me.Off;
		A	#me.FOn;
		AN	#me.OOn;
		=	#me.MOff;
		
		A	#me.FOn;
		A	#me.OOn;
		=	#me.On;
		AN	#me.FOn;
		A	#me.OOn;
		=	#me.MOn;
		JU	TEND;
	Typ5:A	#me.FOff;
		AN	#me.OOn;
		=	#me.Off;
		AN	#me.FOff;
		AN	#me.OOn;
		=	#me.MOff;
		
		A	#me.FOn;
		A	#me.OOn;
		=	#me.On;
		AN	#me.FOn;
		A	#me.OOn;
		=	#me.MOn;
		JU	TEND;
	// --- End Of List --------------------------
	TEND: NOP 0;


	NETWORK
	TITLE = Procesing
		SET;
	// --- Output Control -----------------------

	// Delay On
		A	#me.ACO;
		A	#me.Auto;
		AN	#me._ACO;
		JCN	TONC;
		L	#_TBase;
		L	#me.OnTVal;
		+R;
		T	#me.OnTVal;
		JU	TONR;
	TONC: NOP 0;
		L 	0.0;
		T	#me.OnTVal;
	TONR: NOP 0;
		L	#me.OnTSetP;
		L	#me.OnTVal;
		<=R;
		=	#_OnTd;
		S	#me._ACO;

	// Delay Off
		AN	#me.ACO;
		A	#me._ACO;
		JCN	TOFC;
		L	#_TBase;
		L	#me.OffTVal;
		+R;
		T	#me.OffTVal;
		JU	TOFR;
	TOFC: NOP 0;
		L 	0.0;
		T	#me.OnTVal;
	TOFR: NOP 0;
		L	#me.OffTSetP;
		L	#me.OffTVal;
		<=R;
		=	#_OffTd;
		ON	#me.Rel;
		R	#me._ACO;

	// --- Output Control  ----------------------
		A	#me._ACO;
		A	#me.Auto;
		O;
		A	#me.MCO;
		AN	#me.Auto;
		S	#me.OOn;

		AN	#me.MCO;
		O	#me.Auto;
		AN	#me._ACO;
		ON	#me.Rel;
		R	#me.OOn;

		AN	#me.OOn;
		=	#me.OOff;

	// --- Alarm Evaluation ---------------------
	// Delay Alarm
		A	#me.MOff;
		O	#me.MOn;
		JCN	TALC;
		A	#me.EWrn;
		=	#me.Wrn;

		L	#_TBase;
		L	#me.AlmTVal;
		+R;
		T	#me.AlmTVal;
		JU	TALR;
	TALC: NOP 0;
		=	#me.Wrn;
		L 	0.0;
		T	#me.AlmTVal;
	TALR: NOP 0;
		L	#me.AlmTSetP;
		L	#me.AlmTVal;
		<=R;
		=	#_AlmTd;

		A	#_AlmTd;
		A	#me.EAlm;
		= 	#me.Alm;

		A 	#me.Alm;
		S 	#me.AlmS;


	NETWORK
	TITLE = Finalise
	FIN:	NOP 0;
		
END_FUNCTION

