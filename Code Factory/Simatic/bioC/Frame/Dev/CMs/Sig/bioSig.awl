FUNCTION "bioSig" : Void
TITLE = Batch I/O Digital Signal Processing
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Woermann
VERSION : 0.4
// Batch I/O Digital Signal Processing
// by Woermann Automation
// Date: 2020-07-15
// Description:


	VAR_INPUT 
	END_VAR

	VAR_OUTPUT 
	END_VAR

	VAR_IN_OUT 
		me : "bioSig.DT";
	END_VAR

	VAR_TEMP
		_TBase : Real;	// Time Base (Block Cycle Time in s)
		_OnTd : Bool;	// Delay On Timer Done	
		_OffTd : Bool;	// Delay Off Timer Done	
		_AlmTd : Bool;	// Alarm Timer Done	
	END_VAR


BEGIN
	NETWORK
	TITLE = Initialise
		SET;
	// -- Get Time Base in Seconds
		L	"bioCDT.DB".SysTime.dtS;
		T	#_TBase;

	// -- Presets
		CLR;
		=	#me.MOff;
		=	#me.MOn;

	
	NETWORK
	TITLE = Type Definition
		SET;
	// --- Type Selection -----------------------
		L	#me.Typ;
		JL    NDEF;
		JU    Typ0;	// DIrect
		JU    Typ1;	// Inverted
		JU    Typ2;	// Latched
	NDEF: BEU;
	Typ0:A	#me.Raw;
		=	#me._Sig;
		JU	TEND;
	Typ1:AN	#me.Raw;
		=	#me._Sig;
		JU	TEND;
	Typ2:A	#me.Raw;
		S	#me._Sig;
		A	#me.Res;
		S	#me._Sig;
		JU	TEND;
	// --- End Of List --------------------------
	TEND: NOP 0;


	NETWORK
	TITLE = Procesing
		SET;
	// --- Signal Processing --------------------
	// Delay On
		A	#me._Sig;
		AN	#me.Sig;
		JCN	TONC;
		=	#me.MOn;	
		L	#_TBase;
		L	#me.OnTVal;
		+R;
		T	#me.OnTVal;
		JU	TONR;
	TONC: NOP;
		L 	0.0;
		T	#me.OnTVal;
	TONR: NOP 0;
		L	#me.OnTSetP;
		L	#me.OnTVal;
		<=R;
		=	#_OnTd;
		S	#me.Sig;

	// Delay Off
		AN	#me._Sig;
		A	#me.Sig;
		JCN	TOFC;
		=	#me.MOff;
		L	#_TBase;
		L	#me.OffTVal;
		+R;
		T	#me.OffTVal;
		JU	TOFR;
	TOFC: NOP;
		L 	0.0;
		T	#me.OnTVal;
	TOFR: NOP 0;
		L	#me.OffTSetP;
		L	#me.OffTVal;
		<=R;
		=	#_OffTd;
		R	#me.Sig;

		A	#me.Sig;
		A	#me._Sig;
		=	#me.On;

		AN	#me.Sig;
		AN	#me._Sig;
		=	#me.Off;
		
	// --- Alarm Evaluation ---------------------
	// Delay Alarm
		A	#me.Sig;
		X	#me.Set;
		JCN	TALC;
		A	#me.EWrn;
		=	#me.Wrn;

		L	#_TBase;
		L	#me.AlmTVal;
		+R;
		T	#me.AlmTVal;
		JU	TALR;
	TALC: NOP 0;
		=	#me.Wrn;
		L 	0.0;
		T	#me.AlmTVal;
	TALR: NOP 0;
		L	#me.AlmTSetP;
		L	#me.AlmTVal;
		<=R;
		=	#_AlmTd;

		A	#_AlmTd;
		A	#me.EAlm;
		= 	#me.Alm;

		A 	#me.Alm;
		S 	#me.AlmS;


	NETWORK
	TITLE = Finalise
	FIN:	NOP 0;
		
END_FUNCTION

