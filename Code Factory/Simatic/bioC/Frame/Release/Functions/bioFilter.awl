FUNCTION "bioFilter" : VOID
TITLE = Simple Noise Filter
// -- Woermann Automation --
// Real Value Noise Filter
// Date: 2020-06-21
// Description:
// Val = Val * Alpha + (Raw * (1.0 - Alpha))
// -- Inputs --
// On: If TRUE the Filter is Active; Else the Filtered Value is not changed
// Raw: Input of the Raw Value
// dMin: The defference between Raw Value and Filtered Value has to be bigger 
//than this to be validated; 0 Turns this Function OFF
// dMax: The defference between Raw Value and Filtered Value has to be smaller 
//than this to be validated; 0 Turns this Function OFF
// Alpha: Defines the filtering amount %; 0.0 (0%) no filtering (Val=Raw) 
//Value; 1.0 (100%) full filtering (no update with raw). So the bigger the Alpha 
//Value is, the more smoothing you get
// -- In-/Outputs --
// Val: Filtered Value to be Adjusted by this Function
AUTHOR : Woermann
VERSION : 0.2


VAR_INPUT
  On : BOOL ;	// Activate Filter Function
  Raw : REAL ;	// Raw Value To Be Filtered
  dMin : REAL ;	// Delta Minimum (Too Small Change, No Update of Val, If 0 then Disabled)
  dMax : REAL ;	// Delta Maximum (Too Big Change, Update Val with dMax, If 0 then Disabled)
  Alpha : REAL ;	// Factor for Filter between 0 (0% no Update) and 1 (100% no Damping, Val = Raw)
END_VAR
VAR_IN_OUT
  Val : REAL ;	// Filtered Value
END_VAR
VAR_TEMP
  _rTmp : REAL ;	
  _dVal : REAL ;	
END_VAR
BEGIN
NETWORK
TITLE = Initialise

      SET   ; 
// Acitve Evaluation
      L     #Val; 
      L     DW#16#FF800000; 
      ==D   ; 
      ON    #On; 
      BEB   ; 
      NOP   0; 

      L     #Raw; 
      L     #Val; 
      -R    ; 
      T     #_dVal; 

// Alpha Min Max Constrain
      L     #Alpha; 
      L     1.000000e+00; 
      >R    ; 
      SPBN  AMax; 
      T     #Alpha; 
AMax: NOP   0; 

      L     #Alpha; 
      L     0.000000e+00; 
      <R    ; 
      SPBN  AMin; 
      T     #Alpha; 
AMin: NOP   0; 

NETWORK
TITLE = Filter Algorithm

      SET   ; 
// Check Minimum Delta
      L     #_dVal; 
      ABS   ; 
      L     #dMin; 
      <R    ; 
      SPB   FIN; 
// Check Maximum Delta (Positive)
      L     #dMax; 
      L     0.000000e+00; 
      >R    ; 
      SPBN  CALC; 
      L     #_dVal; 
      L     #dMax; 
      >R    ; 
      SPBN  CALC; 
      L     #Val; 
      +R    ; 
      T     #Raw; 
      SPA   CALC; 
// Check Maximum Delta (Negative)
      L     #_dVal; 
      L     #dMax; 
      <R    ; 
      SPBN  CALC; 
      L     #Val; 
      L     #dMax; 
      -R    ; 
      T     #Raw; 
// Calculate Filtered Value
CALC: L     #Alpha; 
      L     #Val; 
      *R    ; 
      T     #_rTmp; 
      L     1.000000e+00; 
      L     #Alpha; 
      -R    ; 
      L     #Raw; 
      *R    ; 
      L     #_rTmp; 
      +R    ; 
      T     #Val; 

NETWORK
TITLE = Finalise

FIN:  CLR   ; 
// Save Values
      BEA   ; 
END_FUNCTION

